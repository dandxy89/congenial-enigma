// Spec:
// https://www.ibm.com/docs/en/icos/22.1.1?topic=cplex-lp-file-format-algebraic-representation
// https://www.fico.com/fico-xpress-optimization/docs/dms2020-03/solver/optimizer/HTML/chapter10_sec_section102.html
// https://www.gurobi.com/documentation/current/refman/lp_format.html
//

// Common
WHITESPACE   = _{ " " }
POS_INFINITY =  { "+" ~ ^"inf" ~ ^"inity"? }
NEG_INFINITY =  { "-" ~ ^"inf" ~ ^"inity"? }
FLOAT        = ${
    POS_INFINITY
  | NEG_INFINITY
  | "-" ~ ASCII_DIGIT+ ~ ("." ~ ASCII_DIGIT+)?
  | ASCII_DIGIT+ ~ ("." ~ ASCII_DIGIT+)?
}
PLUS         =  { "+" }
MINUS        =  { "-" }
OPERATOR     = _{ PLUS | MINUS }
COLON        = _{ ":" }
ASTERIX      = _{ "*" }
FREE         =  { ^"FREE" }
END          = _{ ^"END" }

// Variable Names
// https://www.ibm.com/docs/en/icos/22.1.1?topic=representation-variable-names-in-lp-file-format
// alphanumeric (a-z, A-Z, 0-9) or one of these symbols: !"#$%&/,.;?@_`'{}()|~'
VALID_CHARS = _{
    ASCII_ALPHANUMERIC
  | "!"
  | "#"
  | "$"
  | "%"
  | "&"
  | "("
  | ")"
  | ","
  | "."
  | ";"
  | "?"
  | "@"
  | "_"
  | "‘"
  | "’"
  | "{"
  | "}"
  | "~"
}
VARIABLE    = ${
    !(FREE | END | SOS_SECTION | CONSTRAINT_SECTION | BOUND_SECTION | GENERALS_SECTION | BINARIES_SECTION | SEMI_CONTINUOUS_SECTION) ~ VALID_CHARS{1, 255}
}

// Problem Name
PROBLEM_NAME    = ${ VALID_CHARS+ }
LP_PROBLEM_NAME = _{ "\\" ~ "Problem name:" ~ PROBLEM_NAME }

// Comments
// https://www.ibm.com/docs/en/icos/22.1.1?topic=representation-comments-in-lp-file-format
// TODO: Problem Name
COMMENT_TEXT = _{ (VALID_CHARS | COLON)* }
COMMENTS     = _{ "\\" ~ ASTERIX? ~ COMMENT_TEXT ~ ASTERIX? ~ NEWLINE? }

// Problem sense in LP file format
// https://www.ibm.com/docs/en/icos/22.1.1?topic=representation-problem-sense-in-lp-file-format
// Example:
MIN_SENSE     =  { ^"MIN" ~ ^"IMIZE"? | ^"MINIMUM" }
MAX_SENSE     =  { ^"MAX" ~ ^"IMIZE"? | ^"MAXIMUM" }
PROBLEM_SENSE = _{ NEWLINE* ~ (MIN_SENSE | MAX_SENSE) }

// Objective function
// https://www.ibm.com/docs/en/icos/22.1.1?topic=representation-objective-in-lp-file-format
// Example: obj:  x1 + 2 x2 + 3.1415
OBJECTIVE_EXPR = {
    OPERATOR? ~ FLOAT? ~ VARIABLE
  | OPERATOR? ~ FLOAT
  | NEWLINE ~ OPERATOR ~ FLOAT ~ VARIABLE
}
OBJECTIVE_NAME = { VALID_CHARS{1, 255} }
OBJECTIVE      = { NEWLINE* ~ (OBJECTIVE_NAME ~ COLON)? ~ OBJECTIVE_EXPR ~ (OBJECTIVE_EXPR)* }
OBJECTIVES     = { OBJECTIVE* }

// Constraints
// https://www.ibm.com/docs/en/icos/22.1.1?topic=representation-constraints-in-lp-file-format
CONSTRAINT_SECTION = _{
    (^"subject to" | ^"such that" | ^"S.T." | ^"st") ~ COLON? ~ NEWLINE?
}
GT                 =  { ">" }
GTE                =  { ">=" }
LT                 =  { "<" }
LTE                =  { "<=" }
EQ                 =  { "=" }
CMP                = _{ GTE | GT | LTE | LT | EQ }
CONSTRAINT_EXPR    =  { OPERATOR? ~ FLOAT? ~ VARIABLE | OPERATOR? ~ FLOAT }
CONSTRAINT_NAME    = ${ VALID_CHARS{1, 255} }
CONSTRAINT         =  {
    NEWLINE? ~ (CONSTRAINT_NAME ~ COLON*)? ~ CONSTRAINT_EXPR ~ (NEWLINE? ~ OPERATOR ~ CONSTRAINT_EXPR)* ~ NEWLINE? ~ CMP ~ FLOAT
}
CONSTRAINTS        =  { NEWLINE* ~ CONSTRAINT_SECTION ~ CONSTRAINT+ }

// Bounds
// https://www.ibm.com/docs/en/icos/22.1.1?topic=representation-bounds-in-lp-file-format
BOUND_SECTION   = _{ ^"bound" ~ ^"s"? }
BOUNDED         =  { FLOAT ~ (LTE | LT) ~ VARIABLE ~ (LTE | LT) ~ FLOAT }
FREE_VARIABLE   =  { VARIABLE ~ FREE }
LOWER_BOUND     =  { VARIABLE ~ GTE ~ FLOAT }
LOWER_BOUND_REV =  { FLOAT ~ GTE ~ VARIABLE }
UPPER_BOUND     =  { VARIABLE ~ LTE ~ FLOAT }
BOUND           = _{
    NEWLINE ~ (FREE_VARIABLE | BOUNDED | UPPER_BOUND | LOWER_BOUND | LOWER_BOUND_REV)
}
BOUNDS          =  { NEWLINE* ~ BOUND_SECTION ~ (BOUND+)? }

// Integers
// A list of variable names of integer variables. Unless otherwise specified in the
// bounds section, the default relaxation interval of the variables is [0, 1].
INTEGER_SECTION = _{ ^"Integers" }
INTEGERS        =  { NEWLINE* ~ INTEGER_SECTION ~ (NEWLINE? ~ VARIABLE)* }

// Generals
// A list of variable names of integer variables. Unless otherwise specified in the
// bounds section, the default relaxation interval of the variables is [0, +Infinity].
GENERALS_SECTION = _{ ^"Gen" ~ ^"eral"? ~ ^"s"? }
GENERALS         =  { NEWLINE* ~ GENERALS_SECTION ~ (NEWLINE? ~ VARIABLE)* }

// Binaries
// A list of variable names of binary variables.
BINARIES_SECTION = _{ ^"Binar" ~ (^"ies" | ^"y") }
BINARIES         =  { NEWLINE* ~ BINARIES_SECTION ~ (NEWLINE? ~ VARIABLE)* }

// Semi-Continuous
// To specify any of the variables as semi-continuous variables, that is as variables that
// may take the value 0 or values between the specified lower and upper bounds
SEMI_CONTINUOUS_SECTION = _{ ^"SEMI" ~ (^"S" | ^"-CONTINUOUS") }
SEMI_CONTINUOUS         =  { NEWLINE* ~ SEMI_CONTINUOUS_SECTION ~ (NEWLINE? ~ VARIABLE)* }

// SOS: Special Ordered Set
SOS_SECTION         = _{ ^"SOS" }
TYPE1               =  { "S1::" }
TYPE2               =  { "S2::" }
VARIABLE_AND_WEIGHT =  { VARIABLE ~ ":" ~ FLOAT }
SOS_CONSTRAINT      =  { NEWLINE? ~ (CONSTRAINT_NAME ~ COLON?)? ~ (TYPE1 | TYPE2) ~ VARIABLE_AND_WEIGHT* }
SOS                 =  { NEWLINE* ~ SOS_SECTION ~ SOS_CONSTRAINT+ }

// End of file
// https://www.ibm.com/docs/en/icos/22.1.1?topic=representation-end-file-in-lp-file-format
EOF = _{ NEWLINE* ~ END }

// Global
OPTIONAL_SECTIONS = _{ BOUNDS? ~ INTEGERS? ~ GENERALS? ~ BINARIES? ~ SEMI_CONTINUOUS? ~ SOS? ~ EOF? }
LP_FILE           =  {
    LP_PROBLEM_NAME? ~ COMMENTS* ~ PROBLEM_SENSE ~ OBJECTIVES ~ CONSTRAINTS ~ OPTIONAL_SECTIONS
}
