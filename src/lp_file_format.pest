// Spec:
// https://www.ibm.com/docs/en/icos/22.1.1?topic=cplex-lp-file-format-algebraic-representation
// https://www.fico.com/fico-xpress-optimization/docs/dms2020-03/solver/optimizer/HTML/chapter10_sec_section102.html
// https://www.gurobi.com/documentation/current/refman/lp_format.html
// 

// Common
WHITESPACE   = _{ " " }
POS_INFINITY =  { "+" ~ ^"inf" ~ ^"inity"? }
NEG_INFINITY =  { "-" ~ ^"inf" ~ ^"inity"? }
FLOAT        =  { POS_INFINITY | NEG_INFINITY | ASCII_DIGIT+ ~ ("." ~ ASCII_DIGIT+)? }
PLUS         =  { "+" }
MINUS        =  { "-" }
OPERATOR     = _{ PLUS | MINUS }
COLON        = _{ ":" }
ASTERIX      = _{ "*" }
FREE         =  { ^"FREE" }
END          = _{ ^"END" }

// Comments
// https://www.ibm.com/docs/en/icos/22.1.1?topic=representation-comments-in-lp-file-format
// TODO: Problem Name
COMMENT_TEXT = _{ (VALID_CHARS | COLON)* }
COMMENTS     = _{ "\\" ~ ASTERIX? ~ COMMENT_TEXT ~ ASTERIX? ~ NEWLINE? }

// Problem sense in LP file format
// https://www.ibm.com/docs/en/icos/22.1.1?topic=representation-problem-sense-in-lp-file-format
// Example:
MIN_SENSE     =  { ^"MIN" ~ ^"IMIZE"? | ^"MINIMUM" }
MAX_SENSE     =  { ^"MAX" ~ ^"IMIZE"? | ^"MAXIMUM" }
PROBLEM_SENSE = _{ NEWLINE* ~ (MIN_SENSE | MAX_SENSE) }

// Variable Names
// https://www.ibm.com/docs/en/icos/22.1.1?topic=representation-variable-names-in-lp-file-format
// alphanumeric (a-z, A-Z, 0-9) or one of these symbols: !"#$%&/,.;?@_`'{}()|~'
VALID_CHARS       = _{
    !(FREE | END | CONSTRAINT_PREFIX) ~ (ASCII_ALPHANUMERIC | "!" | "#" | "$" | "%" | "&" | "(" | ")" | "," | "." | ";" | "?" | "@" | "_" | "‘" | "’" | "{" | "}" | "~")
}
CONSTRAINT_PREFIX = _{
    (^"subject to" | ^"such that" | ^"S.T.") ~ COLON? ~ NEWLINE?
}
VARIABLE          =  { !(FREE | END | CONSTRAINT_PREFIX) ~ VALID_CHARS{1, 255} }

// Objective function
// https://www.ibm.com/docs/en/icos/22.1.1?topic=representation-objective-in-lp-file-format
// Example: obj:  x1 + 2 x2 + 3.1415
OBJECTIVE_EXPR = {
    OPERATOR? ~ FLOAT? ~ VARIABLE
  | OPERATOR? ~ FLOAT
  | NEWLINE ~ OPERATOR ~ FLOAT ~ VARIABLE
}
OBJECTIVE_NAME = { VALID_CHARS{1, 255} }
OBJECTIVE      = { NEWLINE* ~ OBJECTIVE_NAME ~ COLON ~ OBJECTIVE_EXPR ~ (OBJECTIVE_EXPR)* }
OBJECTIVES     = { OBJECTIVE* }

// Constraints
// https://www.ibm.com/docs/en/icos/22.1.1?topic=representation-constraints-in-lp-file-format
GT              =  { ">" }
GTE             =  { ">=" }
LT              =  { "<" }
LTE             =  { "<=" }
EQ              =  { "=" }
CMP             = _{ GTE | GT | LTE | LT | EQ }
CONSTRAINT_EXPR =  { OPERATOR? ~ FLOAT? ~ VARIABLE | OPERATOR? ~ FLOAT }
CONSTRAINT_NAME =  { VALID_CHARS{1, 255} }
CONSTRAINT      =  {
    NEWLINE? ~ CONSTRAINT_NAME ~ COLON? ~ CONSTRAINT_EXPR ~ (NEWLINE? ~ OPERATOR ~ CONSTRAINT_EXPR)* ~ NEWLINE? ~ CMP ~ FLOAT
}
CONSTRAINTS     =  { NEWLINE* ~ CONSTRAINT_PREFIX ~ CONSTRAINT+ }

// Bounds
// https://www.ibm.com/docs/en/icos/22.1.1?topic=representation-bounds-in-lp-file-format
BOUND_PREFIX = _{ ^"bound" ~ ^"s"? }
BOUND        =  {
    NEWLINE ~ (VARIABLE ~ FREE | FLOAT ~ LTE ~ VARIABLE ~ LTE ~ FLOAT | VARIABLE ~ LTE ~ FLOAT | FLOAT ~ LTE ~ VARIABLE)
}
BOUNDS       =  { NEWLINE* ~ BOUND_PREFIX ~ BOUND+ }

// Integers
// A list of variable names of integer variables. Unless otherwise specified in the
// bounds section, the default relaxation interval of the variables is [0, 1].
INTEGER_PREFIX = _{ ^"Integers" }
INTEGERS       =  { NEWLINE ~ INTEGER_PREFIX ~ (NEWLINE ~ VARIABLE)+ }

// Generals
// A list of variable names of integer variables. Unless otherwise specified in the
// bounds section, the default relaxation interval of the variables is [0, +Infinity].
GENERALS_PREFIX = _{ ^"Gen" ~ ^"erals"? }
GENERALS        =  { NEWLINE ~ GENERALS_PREFIX ~ (NEWLINE ~ VARIABLE)+ }

// Binaries
// A list of variable names of binary variables.
BINARIES_PREFIX = _{ ^"Binaries" }
BINARIES        =  { NEWLINE ~ BINARIES_PREFIX ~ (NEWLINE ~ VARIABLE)+ }

// End of file
// https://www.ibm.com/docs/en/icos/22.1.1?topic=representation-end-file-in-lp-file-format
EOF = _{ NEWLINE* ~ END }

// Global
LP_FILE = {
    COMMENTS* ~ PROBLEM_SENSE ~ OBJECTIVES ~ CONSTRAINTS ~ BOUNDS? ~ INTEGERS? ~ GENERALS? ~ BINARIES? ~ EOF
}
